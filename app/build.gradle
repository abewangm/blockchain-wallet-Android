apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'kotlin-android'
apply from: '../jacoco.gradle'
apply from: 'environment.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    dexOptions {
        javaMaxHeapSize "3g"
    }
    defaultConfig {
        applicationId "piuk.blockchain.android"
        targetSdkVersion 25
        versionCode 237
        versionName "6.2.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = "blockchain-$versionName"
        // URLs for Staging, Dev and TestNet Support
        buildConfigField "String", "DEV_BASE_SERVER", devBaseServer
        buildConfigField "String", "STAGING_BASE_SERVER", stagingBaseServer
        buildConfigField "String", "TESTNET_BASE_SERVER", testnetBaseServer
        buildConfigField "String", "DEV_API_SERVER", devApiServer
        buildConfigField "String", "STAGING_API_SERVER", stagingApiServer
        buildConfigField "String", "TESTNET_API_SERVER", testnetApiServer
        buildConfigField "String", "DEV_WEBSOCKET", devWebsocket
        buildConfigField "String", "STAGING_WEBSOCKET", stagingWebsocket
        buildConfigField "String", "TESTNET_WEBSOCKET", testnetWebsocket
        // Flag for Contacts feature
        buildConfigField "boolean", "CONTACTS_ENABLED", "false"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "google_maps_key", "AIzaSyB_5WN2exwvF4pqQ6rW6JdkHcYWjZOim4w"
            multiDexEnabled false
            buildConfigField "boolean", "DOGFOOD", "false"
        }
        debug {
            resValue "string", "google_maps_key", "AIzaSyDOrXqr30C3vjiUM4HMeZO8HFpTIH8CVaM"
            multiDexEnabled true
            buildConfigField "boolean", "DOGFOOD", "false"
        }
        dogfood {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".dogfood"
            versionNameSuffix '-DOGFOOD'
            debuggable true
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "google_maps_key", "AIzaSyB_5WN2exwvF4pqQ6rW6JdkHcYWjZOim4w"
            multiDexEnabled false
            buildConfigField "boolean", "DOGFOOD", "true"
        }
    }
    productFlavors {
        prod {
            // For release and testing on <API21 devices
            minSdkVersion 14
        }
        dev {
            // For everyday work, setting minSdkVersion to 21 bypasses MultiDex, resulting in
            // much faster builds
            minSdkVersion 21
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError false
        fatal 'StopShip'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    dataBinding {
        enabled = true
    }
    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
        reports.junitXml.destination = file('../build/test-results/')
        // This allows "unstable" builds on CI rather than straight failures
        ignoreFailures true
        // Required for correct coverage of Robolectric tests
        jacoco {
            includeNoLocationClasses = true
        }
    }
    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
    }
}

// Ensure no Kotlin code is present in the production app... for now.
afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

dependencies {
    compile files('libs/MyWalletHD.jar')
    compile group: 'info.blockchain', name: 'api-client', version: '0.1.4-SNAPSHOT'
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:support-v13:$supportVersion"
    compile "com.android.support:preference-v7:$supportVersion"
    compile "com.android.support:preference-v14:$supportVersion"
    // Enable crash reporting only for release version
    releaseCompile "com.google.firebase:firebase-crash:$googleServicesVersion"
    compile "com.google.firebase:firebase-messaging:$googleServicesVersion"
    compile "com.google.android.gms:play-services-maps:$googleServicesVersion"
    compile 'org.bitcoinj:bitcoinj-core:0.13.4'
    compile 'commons-io:commons-io:2.4'
    // Keep at 1.3 to match Android implementation
    compile 'commons-codec:commons-codec:1.3'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.android.support:multidex:1.0.1'
    compile 'org.thoughtcrime.ssl.pinning:AndroidPinning:1.0.0'
    compile 'com.neovisionaries:nv-websocket-client:1.19'
    compile('com.google.zxing:core:3.3.0') { transitive = true }
    compile 'com.github.mukeshsolanki:country-picker-android:1.1.7'
    compile 'com.aurelhubert:ahbottomnavigation:2.0.2'
    // URL Builder
    compile "io.mikael:urlbuilder:2.0.8"
    // RxJava
    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
    // OkHttp
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    // Dagger2
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor 'com.google.guava:guava:20.0'
    // Fingerprint authentication
    compile 'com.mtramin:rxfingerprint:2.0.3'
    // Custom fonts
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    // GSON
    compile 'com.google.code.gson:gson:2.8.0'

    // UI/Instrumentation Tests
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile "org.mockito:mockito-core:$mockitoVersion"
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.6.3'
    androidTestCompile "com.android.support:support-annotations:$supportVersion"
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'javax.inject', module: 'support-annotations'
    }

    // Unit Tests
    testCompile 'org.json:json:20140107'
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testCompile('org.robolectric:robolectric:3.2.2') {
        exclude group: 'com.google.guava'
        exclude group: 'com.google.protobuf'
    }
    // Unit Tests in Kotlin
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testCompile 'com.nhaarman:mockito-kotlin:1.2.0'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

// Required for RetroLambda && CountryPicker
repositories {
    jcenter()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url nexusUrl + '/repository/maven-public/'
        credentials(PasswordCredentials) {
            username nexusUsername
            password nexusPassword
        }
    }
}

// This must remain at the bottom of this file until Google work out a better way to do this
apply plugin: 'com.google.gms.google-services'